module openmeteo;

import std.stdio;

import std.conv;
import std.variant;
import std.array : join;
import std.json : JSONValue;
import std.algorithm.searching : canFind;

import curl = std.net.curl;

private immutable string[] PARAM_ARRAY_KEYS = ["daily", "hourly", "current"];

private immutable string[] MATRIX_ARRAY_KEYS = ["daily", "hourly"];

/**
 * Represents weather description and icon for a given weather code.
 */
struct WeatherCodeInfo
{
    string description;
    string icon;
}

/**
 * An associative array mapping weather codes to their corresponding descriptions and icons.
 */
private immutable WeatherCodeInfo[int] WEATHER_CODE_INFO = [
     0: WeatherCodeInfo("Clear sky",                     "â˜€ï¸"),
     1: WeatherCodeInfo("Mainly clear",                  "ğŸŒ¤ï¸"),
     2: WeatherCodeInfo("Partly cloudy",                 "â›…"),
     3: WeatherCodeInfo("Overcast",                      "â˜ï¸"),
    45: WeatherCodeInfo("Fog",                           "ğŸŒ«ï¸"),
    48: WeatherCodeInfo("Depositing Rime Fog",           "ğŸŒ«ï¸"),
    51: WeatherCodeInfo("Light drizzle",                 "ğŸŒ¦ï¸"),
    53: WeatherCodeInfo("Moderate drizzle",              "ğŸŒ¦ï¸"),
    55: WeatherCodeInfo("Dense drizzle",                 "ğŸŒ¦ï¸"),
    56: WeatherCodeInfo("Light freezing drizzle",        "ğŸŒ§ï¸"),
    57: WeatherCodeInfo("Dense freezing drizzle",        "ğŸŒ§ï¸"),
    61: WeatherCodeInfo("Light rain",                    "ğŸŒ¦ï¸"),
    63: WeatherCodeInfo("Moderate rain",                 "ğŸŒ§ï¸"),
    65: WeatherCodeInfo("Heavy rain",                    "ğŸŒ§ï¸"),
    66: WeatherCodeInfo("Light freezing rain",           "ğŸŒ§ï¸"),
    67: WeatherCodeInfo("Heavy freezing rain",           "ğŸŒ§ï¸"),
    71: WeatherCodeInfo("Slight snow fall",              "ğŸŒ¨ï¸"),
    73: WeatherCodeInfo("Moderate snow fall",            "ğŸŒ¨ï¸"),
    75: WeatherCodeInfo("Heavy snow fall",               "â„ï¸"),
    77: WeatherCodeInfo("Snow grains",                   "ğŸŒ¨ï¸"),
    80: WeatherCodeInfo("Slight rain showers",           "ğŸŒ¦ï¸"),
    81: WeatherCodeInfo("Moderate rain showers",         "ğŸŒ§ï¸"),
    82: WeatherCodeInfo("Violent rain showers",          "â›ˆï¸"),
    85: WeatherCodeInfo("Slight snow showers",           "ğŸŒ¨ï¸"),
    86: WeatherCodeInfo("Heavy snow showers",            "â„ï¸"),
    95: WeatherCodeInfo("Thunderstorm",                  "â›ˆï¸"),
    96: WeatherCodeInfo("Thunderstorm with slight hail", "â›ˆï¸"),
    99: WeatherCodeInfo("Thunderstorm with heavy hail",  "â›ˆï¸")
];

/**
 * Retrieves the weather description and icon for a given weather code.
 * Params:
 *   code: (`int`) The weather code for which to retrieve information.
 * Returns:
 *   A `WeatherCodeInfo` struct containing the description and icon for the given weather code.
 */
WeatherCodeInfo getWeatherCodeInfo(int code)
{
    if (code in WEATHER_CODE_INFO)
    {
        return WEATHER_CODE_INFO[code];
    }
    else
    {
        return WeatherCodeInfo("Unknown", "â“");
    }
}

/**
 * Makes a GET request to the specified open-meteo API URL with the given parameters and returns the response as a string.
 * Params:
 *   url: (`string`) The base URL of the open-meteo API endpoint.
 *   parameters: (`Variant[string]`) An associative array of query parameters to include in the API request. The values can be either strings or arrays of strings.
 * Returns:
 *   A `string` containing the response from the open-meteo API.
 */
string weatherApi(string url, Variant[string] parameters)
{
    string paramString = "";
    foreach (kv; parameters.byKeyValue)
    {
        paramString ~= kv.key ~ "=";

        if (canFind(PARAM_ARRAY_KEYS, kv.key))
        {
            paramString ~= kv.value.get!(string[]).join(",");
        }
        else
        {
            paramString ~= kv.value.to!string;
        }

        paramString ~= "&";
    }

    return cast(string)curl.get(url ~ "?" ~ paramString);
}

private JSONValue[] recordsFromMatrix(JSONValue matrix)
{
    size_t n = matrix["time"].array.length; // all the arrays in the matrix like structure are the same length
    JSONValue[] records;

    for (size_t i = 0; i < n; i++)
    {
        JSONValue record = JSONValue.init;
        foreach (key, arr; matrix.object)
        {
            record[key] = arr.array[i];
        }
        records ~= record;
    }

    return records;
}

/**
 * Converts the matrix-like structure returned from open-meteo to arrays of objects.
 *
 * The open-meteo API returns data like the following:
 * ---
 * {
 *   "daily": {
 *     "time": ["2024-06-01", "2024-06-02", "2024-06-03"],
 *     "temperature_2m_max": [25.0, 26.5, 24.0],
 *     "temperature_2m_min": [15.0, 16.0, 14.5]
 *   }
 * }
 * ---
 * This function transforms it into the following format:
 * ---
 * {
 *   "daily": [
 *     {
 *       "time": "2024-06-01",
 *       "temperature_2m_max": 25.0,
 *       "temperature_2m_min": 15.0
 *     },
 *     {
 *       "time": "2024-06-02",
 *       "temperature_2m_max": 26.5,
 *       "temperature_2m_min": 16.0
 *     },
 *     {
 *       "time": "2024-06-03",
 *       "temperature_2m_max": 24.0,
 *       "temperature_2m_min": 14.5
 *     }
 *   ]
 * }
 * ---
 * Params:
 *   data: (`JSONValue`) Parsed JSON data
 * Returns:
 *   A new `JSONValue` with the same data in an array of objects.
 */
JSONValue deMatrixData(JSONValue data)
{
    JSONValue result = JSONValue.init;

    foreach (key, value; data.object)
    {
        if (canFind(MATRIX_ARRAY_KEYS, key))
        {
            result[key] = recordsFromMatrix(value);
        }
        else
        {
            result[key] = value;
        }
    }

    return result;
}
